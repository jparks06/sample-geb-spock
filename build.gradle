apply plugin: "groovy"
apply plugin: "eclipse"

defaultTasks "clean", "test"

repositories {
    mavenCentral()
}
 
configurations {
    testCompile.transitive = true
}
 
dependencies {
    groovy "org.codehaus.groovy:groovy-all:1.7.10"
 
    def gebVersion = "0.6.0"
    def seleniumVersion = "2.0.0"
 
    // If using Spock, need to depend on geb-spock
    testCompile "org.codehaus.geb:geb-spock:$gebVersion"
    testCompile "org.spockframework:spock-core:0.5-groovy-1.7"
 
    // If using JUnit, need to depend on geb-junit (3 or 4)
    testCompile "org.codehaus.geb:geb-junit4:$gebVersion"
    testCompile "junit:junit-dep:4.8.2"
 
    // Need a driver implementation
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-ie-driver:$seleniumVersion"
}

// Encoding

def defaultEncoding = 'UTF-8'

compileJava {
    options.encoding = defaultEncoding
}
compileTestJava {
    options.encoding = defaultEncoding
}
compileGroovy {
    groovyOptions.encoding = defaultEncoding 
    options.encoding = defaultEncoding
}
compileTestGroovy {
    groovyOptions.encoding = defaultEncoding
    options.encoding = defaultEncoding
}

test {
    systemProperties "geb.build.reportsDir": "$reportsDir/geb"
    systemProperties "geb.build.baseUrl": "http://www.google.co.jp/"
    systemProperties "groovy.source.encoding":defaultEncoding
    systemProperties "file.encoding":defaultEncoding
    if (rootProject.hasProperty("browser.bin")) {
        systemProperties "webdriver.firefox.bin":rootProject["browser.bin"]
    }
    if (rootProject.hasProperty("env")) {
        systemProperties "geb.env":rootProject["env"]
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.0-milestone-3"
}

